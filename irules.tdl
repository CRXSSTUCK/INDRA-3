;;; -*- Mode: TDL; Coding: utf-8 -*-
;;; Inflecting Lexical Rule Instances
nya-suffix-1 :=
%suffix (* nya)
nya-lex-rule & 
 [ DTR.SYNSEM.LOCAL [ CONT.HOOK [ INDEX #index,
				  LTOP #larg ] ],
   C-CONT [ HOOK [ INDEX #index,
		   LTOP #ltop ],
	    HCONS <! qeq & [ HARG #harg,
			     LARG #larg ] !>,
	    RELS <! quant-relation & [ PRED the_q_rel,
				       LBL #ltop,
				       ARG0 #index,
				       RSTR #harg ] !> ] ].

nya-suffix-2 :=
%suffix (* nya)
nya-lex-rule & 
 [ DTR.SYNSEM.LOCAL [ CONT.HOOK [ INDEX #index,
				  LTOP #larg ] ],
   C-CONT [ HOOK [ INDEX #index ],
	    HCONS <! qeq & [ HARG #harg,
			     LARG #larg ],
		   qeq & [ HARG #harg2,
			   LARG #larg2 ] !>,
	    RELS <! quant-relation & [ PRED def_q_rel,
				       ARG0 #index,
				       RSTR #harg ],
		    arg12-relation & [ PRED poss_rel,
				       LBL #larg,
				       ARG0 event,
				       ARG1 #index,
				       ARG2 #arg2 ],
		    quant-relation & [ PRED pronoun_q_rel,
				       ARG0 #arg2,
				       RSTR #harg2 ],
  		    arg0-relation & [ PRED pron_rel,
				      LBL #larg2,
				      ARG0 #arg2 ] !> ] ].

; <type val="act-prefix">
; <description>Active voice with |me-| prefix
; <ex>menunggu
; <nex>
; <todo>
; </type>
act-prefix :=
%prefix (p mem) (pr mempr) (b memb) (m mem) (f memf) (w mew) (t men) (tr mentr) (d mend) (n men) (v memv) (r mer) (s meny) (sy mensy) (sw mensw) (z menz) (l mel) (c menc) (j menj) (ny meny) (y mey) (k meng) (kh mengkh) (kl mengkl) (g mengg) (ng meng) (q mengq) (h mengh) (a menga) (i mengi) (u mengu) (e menge) (o mengo)
act-lex-rule.

pas-prefix :=
%prefix (* di)
pas-lex-rule.
